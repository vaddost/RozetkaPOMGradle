plugins {
    id 'java'
    id "io.qameta.allure" version "2.9.6"
}

allure {
    version = '2.2.1'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.seleniumhq.selenium:selenium-java:4.1.2'
    implementation 'org.testng:testng:7.5'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.seleniumhq.selenium:selenium-chrome-driver:4.1.2'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:3.0.2'
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

task printJavaVersion(type: PrintPropertyTask) {
    property = 'java.version'
}

task printOsName(type: PrintPropertyTask){
    property = "os.name"
}

class PrintPropertyTask extends DefaultTask {
    @Input
    String property

    @TaskAction
    void printProperty() {
        println System.getProperty(property)
    }
}


